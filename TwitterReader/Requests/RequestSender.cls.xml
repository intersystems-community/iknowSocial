<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="TwitterReader.Requests.RequestSender">
<Description>
This class provides ability to send http-requests (actually https) to twitter api server.</Description>
<Super>%RegisteredObject</Super>
<TimeCreated>63468,61996.197714</TimeCreated>

<Parameter name="SSLConfigurationName">
<Description>
The name of the Cache SSL/TLS configuration to use.
See:
http://localhost:57772/csp/docbook/DocBook.UI.Page.cls?KEY=GCAS_ssltls#GCAS_ssltls_createedit</Description>
<Default>Twitter</Default>
</Parameter>

<Parameter name="RequestTest">
<Description><![CDATA[
0 => request made, no request printout, 1 => request printout, no request, 2 => request printout and request made]]></Description>
<Default>0</Default>
</Parameter>

<Method name="SendHttpRequest">
<Description>
Sends 'GET https://server/location?parametersString' request. Response is saved into responseText.</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[server:%String="api.twitter.com",location:%String,&responseText:%String(MAXLEN=1000000),parameters:%ArrayOfDataTypes="",useSsl:%Boolean=0,authorization:%String="",postArray:%ArrayOfDataTypes=""]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	#dim req as %Net.HttpRequest = ##class(%Net.HttpRequest).%New()
	set req.Server = server
	set req.Location = location
	if (authorization '= "")
	{
		set req.Authorization = authorization
	}
	if (useSsl)
	{
		set req.Https = 1
		set req.SSLConfiguration = ..#SSLConfigurationName
	}
	#dim k as %String = ""
	#dim v as %String = ""
	if (parameters '= "")
	{
		set k = ""
		while (1)
		{
			set v = parameters.GetNext(.k)
			quit:k=""
			do req.InsertParam(k, v)
		}
	}
	#dim test as %Integer = ..#RequestTest	// 0 => request made, no request printout, 1 => request printout, no request, 2 => request printout and request made
	if (test '= 0)
	{
		write "Request:",!
		write "Server: ", req.Server, ", Location: ", req.Location, !
	}
	#dim status as %Status = $$$OK
	#dim asPost as %Boolean = 0
	if (postArray '= "")
	{
		set asPost = 1
		set k = ""
		while (1)
		{
			set v = postArray.GetNext(.k)
			quit:k=""
			do req.InsertFormData(k, v)
		}
	}
	if (asPost)
	{
		set status = req.Post(location, test)
	}
	else
	{
		set status = req.Get(location, test)
	}
	if ($$$ISOK(status))
	{
		#dim response as %String = ""
		if (test '= 1)
		{
			#dim res as %Net.HttpResponse = req.HttpResponse
			if ($data(res))
			{
				write "Response: ", res.StatusCode, ", ", res.StatusLine, ", ", res.ReasonPhrase, !
			}
			if ($$$ISOK(status))
			{
				#dim responseLength as %Integer = 1000000
				set response = res.Data.Read(.responseLength)
				write "Response type: ", $classname(res.Data), !
				write "Response length: ", $length(response), ", ", responseLength, !
			}
		}
		set responseText = response
	}
	else
	{
		set responseText = ""
	}
	quit status
]]></Implementation>
</Method>
</Class>
</Export>
