<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="TWReader.UIBroker">
<Super>Util.UIBroker</Super>
<TimeCreated>63623,17389.188202</TimeCreated>

<Parameter name="ContextType">
<Type>%String</Type>
<Default>TW</Default>
</Parameter>

<Method name="DrawTrends">
<ClassMethod>1</ClassMethod>
<FormalSpec>fields:%String="",tableName:%String="#trends-table",lat:%String,long:%String</FormalSpec>
<Implementation><![CDATA[
	s response=""
	do ##class(TWReader.Requests.APIMethodsCaller).GetPlaceByCoords(.place,lat,long)
	set woeid = place.GetAt(1)."woeid"
	do ##class(TWReader.Requests.APIMethodsCaller).GetTrendsByPlaceJSON(.response,woeid)
	
	set response = $replace(response,"[{","{",1,1)
	set response = $replace(response,"}]}]","}]}")
	set response = $replace(response,"name","sourceId")
	
	&js<
		trends = (#(response)#).trends;
		drawTable('#(tableName)#', trends, #(fields)#,'#');
	>
]]></Implementation>
</Method>

<Method name="AddMorePostsByCoords">
<ClassMethod>1</ClassMethod>
<FormalSpec>shouldClearDomain:%String,numberOfSources:%String,lat:%Float,long:%Float</FormalSpec>
<Implementation><![CDATA[
	if (##class(Util.Misc).IsInteger(numberOfSources)) {
		if (numberOfSources > ##class(TWReader.Requests.APIMethodsCaller).%GetParameter("MaxEntityQueryCount")) {
			set numberOfSources = ##class(TWReader.Requests.APIMethodsCaller).%GetParameter("MaxEntityQueryCount")
		}
		set context = ##class(TWReader.Data.Context).%New(shouldClearDomain)
		///Get Place object by coordinates
		do ##class(TWReader.Requests.APIMethodsCaller).GetPlaceByCoords(.place,lat,long)
		///A WOEID is a Yahoo! Where On Earth ID.
		set woeid = place.GetAt(1)."woeid"
		///Get trends by woeid (set 3 param equal to hashtags will remove all hashtags from the trends list.)
		do ##class(TWReader.Requests.APIMethodsCaller).GetTrendsByPlace(.response,woeid,"")
		set trends = ##class(%ArrayOfObjects).%New()
		set trends = response.GetAt(1)."trends"
		set dt = $zdt($h, 3)
		for i = 1:1:trends.Count() {
			set trend = ##class(TWReader.Data.Trend).%New()
			set trend.name = trends.GetAt(i)."name"
			set trend.date = dt
			set trend.rank = i
			d trend.%Save()
			
			do context.AddMorePosts(trend.name, numberOfSources)
		}
	} else {
		&js<console.log("Incorrect value for number of posts.")>
	}
	do ..DrawTable()
	&js<
		$("#add-tweets-by-trends-modal").modal('hide');
	   	$("#add-tweets-by-trends-modal .progress").css("display", "none");
	    $("#add-tweets-button").prop('disabled', false);
	>
]]></Implementation>
</Method>

<Method name="SaveTrends">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[
	set query = "select * from TWReader_Data.Trend order by name"
	set statement = ##class(%SQL.Statement).%New()
	set status = statement.%Prepare(query)
	set result = statement.%Execute()
	if ('result.%Next()) {
		quit
	}
	set prevName = result.name
	set tUnixtime = ##class(Util.Misc).ConvertTrendsDateTimeToUnixTime(result.date)
	set trends = "[{""name"":""" _ prevName _ """, ""data"":[" _ "{""x"":" _ tUnixtime _ ", ""y"":" _ (11 - result.rank) _ "}"
	while result.%Next() {
		set name = result.name
		if (name '= prevName) {
			set prevName = name
			set trends = trends _ "]}, {""name"":""" _ prevName _ """, ""data"":["
		} else {
			set trends = trends _ ","
		}
		set tUnixtime = ##class(Util.Misc).ConvertTrendsDateTimeToUnixTime(result.date)
		set trends = trends _ "{""x"":" _ tUnixtime _ ", ""y"":" _ (11 - result.rank) _ "}"
		
	}
	set trends = trends _ "]}]"
	&js<
		trendsMutualData = #(trends)#
		trendCallback();
	>
]]></Implementation>
</Method>
</Class>
</Export>
